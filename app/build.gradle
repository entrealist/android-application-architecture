apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'
defaultTasks 'dependencyUpdates'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdkVersion rootProject.ext.compileSdkVersion

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }

        debug{
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
            storeFile file(keystoreProperties['debugStoreFile'])
            storePassword keystoreProperties['debugStorePassword']
        }
    }

    defaultConfig {
        applicationId "com.rosberry.android.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "debug"
            debuggable true

            signingConfig signingConfigs.debug
        }

        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }

            debuggable false

            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "default"

    productFlavors {
        demo {
            dimension "default"

            minSdkVersion 21
            buildConfigField 'String', 'BASE_URL', rootProject.ext.constants.baseDevUrl
            buildConfigField 'String', 'RANDOM', rootProject.ext.constants.random
        }

        prod {
            dimension "default"

            minSdkVersion 19
            buildConfigField 'String', 'BASE_URL', rootProject.ext.constants.baseProdUrl
            buildConfigField 'String', 'RANDOM', rootProject.ext.constants.random
        }
    }

    sourceSets {
        main.java.srcDirs += ['src/main/kotlin']
        test.java.srcDirs += ['src/test/kotlin']
    }

    aaptOptions {
        setCruncherEnabled(false)
    }

    lintOptions {
        checkReleaseBuilds false
        quiet true
        enable "MissingPermission"
        abortOnError false
    }

    packagingOptions {
        pickFirst 'META-INF/rxjava.properties'
    }

    configurations.all {
        resolutionStrategy.force rootProject.ext.dependencies.supportLint
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def deps = rootProject.ext.dependencies
    def testDeps = rootProject.ext.testDependencies

    testImplementation testDeps.jUnit

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.supportAppCompat
    implementation deps.supportv13
    implementation deps.supportRecyclerView
    implementation deps.supportCardView
    implementation deps.supportConstraintLayout
    implementation deps.supportMultidex
    implementation deps.supportMultidexInst

    //kotlin
    implementation deps.kotlin
    implementation deps.ankoCommons
    implementation deps.ankoCoroutines

    //rx
    implementation deps.rxAndroid
    implementation deps.rxJava
    implementation deps.rxPermissions
    implementation deps.rxKotlin

    //inject
    implementation deps.dagger
    kapt deps.daggerCompiler
    kapt deps.daggerAnnotation

    //rest
    implementation deps.retrofit
    implementation deps.retrofitRxJavaAdapter
    implementation deps.retrofitGsonConverter
    implementation deps.okHttpLoggingInterceptor
    implementation deps.okHttp
    implementation deps.googleGson

    //ui
    implementation(deps.materialDialogs) { transitive = true }

    //state
    implementation deps.androidState
    kapt deps.androidStateCompiler

    //media
    implementation deps.picasso

    //moxy
    implementation deps.moxy
    implementation deps.moxySupport
    kapt deps.moxyCompiler

}


